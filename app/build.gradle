apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.huawei.agconnect'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "it.antonino.palco"
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 1
        versionName "1.7"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "config", "version"

    productFlavors {

        huawei {
            dimension "config"
        }

        google {
            dimension "config"
        }

        free {
            versionCode 1
            dimension "version"
            versionNameSuffix "-free"
            buildConfigField "boolean", "BUY_TICKET", "false"
        }

        paid {
            versionCode 1
            dimension "version"
            versionNameSuffix "-paid"
            buildConfigField "boolean", "BUY_TICKET", "true"
        }

    }

    sourceSets {

        googleFree {
            java.srcDirs = ['src/google/java','src/free/java']
            res.srcDirs = ['src/main/res','src/free/res']
            manifest.srcFile 'src/google/AndroidManifest.xml'
        }

        googlePaid {
            java.srcDirs = ['src/google/java','src/paid/java']
            res.srcDirs = ['src/main/res','src/paid/res']
            manifest.srcFile 'src/google/AndroidManifest.xml'
        }

        huaweiFree {
            java.srcDirs = ['src/huawei/java','src/free/java']
            res.srcDirs = ['src/main/res','src/free/res']
            manifest.srcFile 'src/huawei/AndroidManifest.xml'
        }

        huaweiPaid {
            java.srcDirs = ['src/huawei/java','src/paid/java']
            res.srcDirs = ['src/main/res','src/paid/res']
            manifest.srcFile 'src/huawei/AndroidManifest.xml'
        }

    }

    buildTypes.each {
        it.buildConfigField('String', 'BASE_URL', baseUrl)
        it.buildConfigField('String', 'DISCOGS_API_KEY', dicogsApiKey)
        it.buildConfigField('String', 'DISCOGS_SECRET', discogsSecret)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.fragment:fragment-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'org.koin:koin-core:2.1.6'
    implementation 'org.koin:koin-androidx-viewmodel:2.1.6'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.github.sundeepk:compact-calendar-view:3.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    // Calendar
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    // Animation
    implementation 'com.airbnb.android:lottie:3.0.7'
    implementation 'com.github.tarek360.RichPath:animator:0.1.1'
    // Images
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    // Firebase SDK
    googleImplementation platform('com.google.firebase:firebase-bom:26.8.0')
    googleImplementation 'com.google.firebase:firebase-analytics'
    googleImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    googleImplementation 'com.google.firebase:firebase-messaging-ktx'
    googleImplementation 'com.google.firebase:firebase-messaging-directboot:21.0.1'
    // Google Login API
    googleImplementation 'com.google.android.gms:play-services-auth:19.0.0'
    // Facebook Login API
    huaweiImplementation 'com.facebook.android:facebook-android-sdk:4.42.0'
    // HMS
    huaweiImplementation 'com.huawei.agconnect:agconnect-core:1.4.0.300'
    huaweiImplementation 'com.huawei.hms:push:5.0.2.301'
    huaweiImplementation 'com.huawei.hms:location:3.0.3.300'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}